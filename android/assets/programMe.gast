#javaj#

    <frames>
      main, ProgramMe

    <layout of main>
      EVA, 7,7, 4, 4

          , X
          , lProgramMe
          , bINTRO_Pme
          , bSHOW CONNECTION URL
        X , oConsola
          , bDISCONNECT_Pme

   <sysDefaultFonts>
      Consolas, 16, 0, TextArea.font
      Verdana,  16, 0, Button, ToggleButton, Label

#data#

   <bINTRO_Pme> Intro to ProgramMe

   <LABEL DISCONNECT> //DISCONNECT
   <LABEL CONNECT>    //CONNECT

   <cDX> 34
   <cDY> 34

   <SPECIAL_HELLO> //(hello template)

#listix#

   <-- javaj exit>
      micohttp, stop, APPLAPIZ

   <main>
      -->, bDISCONNECT_Pme data!,, @<LABEL DISCONNECT>
      -->, lProgramMe data!,, ProgramMe listening on port @<PORTOS>
      LSX, START_LISTENING

   <PORTOS> 7676

   <-- bINTRO_Pme>
      -->, oConsola clear
      LSX, intro

   <-- bSHOW CONNECTION URL>
      -->, oConsola clear
      //
      //try one of these urls in your browser
      //
      http://@<:lsx host name>:@<PORTOS>
      http://@<:lsx host ip>:@<PORTOS>

   <START_LISTENING>
      -->, oConsola clear
      micohttp, start, APPLAPIZ, @<PORTOS>
            , VERBOSE LEVEL, 2
            , IGNOREBINDEXCEPTION, 1

   <-- bDISCONNECT_Pme>
      IN CASE, @<bDISCONNECT_Pme>
           , @<LABEL DISCONNECT>, micohttp, STOP, APPLAPIZ
           , @<LABEL DISCONNECT>, -->, bDISCONNECT_Pme data!,, @<LABEL CONNECT>
           , @<LABEL CONNECT>, LSX, START_LISTENING
           , @<LABEL CONNECT>, -->, bDISCONNECT_Pme data!,, @<LABEL DISCONNECT>


   <! ----- ((( ----------- initial page
   <GET />
      LSX, index_html

   <! ----- ((( ----------- get data of specific contents by id
   <POST /getDataForId>
      LSX, getContentOf @<id>

   <!
   <! ----- ((( ----------- test current script in target
   <POST /testgast>
      GAST, @<_bodyMemFileName>

   <! ----- ((( ----------- save current script in target
   <POST /savegast>
      CHECK, <>, @<ajaxREQ-gastName>, ""
      CHECK, <>, @<ajaxREQ-gastName>, programMe.gast
      FILEUTIL, COPY, @<_bodyMemFileName>, @<ajaxREQ-gastName>
      LSX, LOAD_DIR, @<ajaxREQ-gastName>/..

   <getContentOf dDir>
      //eDir:
      LSX, SOLVED_DIR, @<dir>
      //
      //:
      //
      LSX, LOAD_DIR, @<dir>

   <getContentOf xFileContent>
      //eFileName:@<fileName>
      //:
      //
      LSX, tryLoadFile, @<fileName>

   <getContentOf checkFileName>
      //fileName allowed:@<ALLOWED_FILENAME>
      //fileName exists:@<EXISTS_FILENAME>
      //:
      //1

   <ALLOWED_FILENAME>
      <! just don't allow to overwrite these two scripts from programMe for "safety comfort"
      CHECK, <>, "autoStart.gast", @<fileName>, 0
      CHECK, <>, "programMe.gast", @<fileName>, 0
      1

   <EXISTS_FILENAME>
      CHECK, FILE, @<fileName>, 0
      1

   <LOAD_DIR>
      VAR=, eldir, @<SOLVED_DIR>
      IN CASE, @<elDir>
             , "..", LSX, viewDir, "."
             , ""  , LSX, viewDir, "."
             , ELSE, CHECK, DIR, @<elDir>
             , ELSE, LSX, viewDir, @<elDir>

   <SOLVED_DIR>
      STRCONV, PATH-SOLVE, @<p1>

   <tryLoadFile>
      CHECK, !=, @<p1>, @<SPECIAL_HELLO>, @<:infile demo/justHello.gast>
      CHECK, FILE, @<p1>
      IN FILE, @<p1>
             , SOLVE, 0


   <DIMSVG> 40
   <ELEM_FILE_DOWNLOAD> //<a href="/@<FULLNORM>"> <div style="width:@<DIMSVG>; height:@<DIMSVG>" class="Graffiti" id="gText"/></a>
   <ELEM_FILE_EDIT>     //<button onclick="loadAndOverwriteFileContent ('@<FULLNORM>')">load</button>


   <viewDir>
     VAR=, DIRLABEL, @<p1>
     // <p id="dirlabel">Path : @<p1>
     IN CASE, @<p1>
            , "."   , //
            , ELSE  , //  <button onclick="luix.AJAXgetDataForId ('dDir', 'dir=@<DIRLABEL>/..', true)">back</button>
     //</p>
     // <table>
     //
     LOOP, DIRS, @<DIRLABEL>
         , RECURSIVE, 0
         ,, //<tr> <td> <div style="width:@<DIMSVG>; height:@<DIMSVG>" class="Graffiti" id="gFolder" onclick="luix.AJAXgetDataForId ('dDir', 'dir=@<FULLNORM>', true)"/> <td>  @<fileName> </tr>

     //
     LOOP, FILES, @<DIRLABEL>, gast
         , RECURSIVE, 0
         ,, CHECK, <>, @<fileName>, programMe.gast
         ,, CHECK, <>, @<fileName>, autoStart.gast
         ,, // <tr> <td> @<ELEM_FILE_DOWNLOAD> <td> @<ELEM_FILE_EDIT> <td> @<fileName></tr>
     // </table>

   <FULLNORM>  //@<:linux-path DIRLABEL>/@<:linux-path fullSubPath>

   <index_html>
      //<!DOCTYPE html>
      //<html>
      //  <head>
      //    <title>Demo jGastona</title>
      //     <style type="text/css">
      //
      LSX, programMe_CSS
      //
      //    </style>
      //  </head>
      //<body>
      //
      //<script>
      //
      //@<:infile META-GASTONA/js/jGastonaEva-min.js>
      //
      //@<:infile META-GASTONA/js/trazos2D-min.js>
      //
      //@<graffiti data>
      //
      //   var luix = jGastona (evaFileUTF82obj ("@<:encode-utf8 programMe_jast>"));
      //   var isFileContentDirty = false;
      //
      //   function setToDirty (dirty) {
      //       isFileContentDirty = dirty;
      //   }
      //
      //   function loadAndOverwriteFileContent (pathToLoad) {
      //      if (isFileContentDirty && false == confirm("Load " + pathToLoad + " and overwrite edition ?"))
      //         return; // do nothing!
      //
      //      luix.AJAXgetDataForId('xFileContent', 'fileName=' + pathToLoad, true);
      //      setToDirty (false);
      //   }
      //
      //   function updateFileNameCheck () {
      //      var allow = luix.getDataCell ("fileName allowed");
      //      var exists = luix.getDataCell ("fileName exists");
      //      if (! allow || !exists) return;
      //      var ele = document.getElementById ("eFileName");
      //      ele.style["background-color"] = (allow==="0" ? "red": (exists === "1" ? "#FAA0B7": "#A6FC7C"));
      //   }
      //
      // </script>
      //
      //</body>
      //</html>
      //

   <programMe_jast>
      //#javaj#
      //
      //   <layout of main>
      //
      //      Eva, 30, 30, 7, 7
      //      -- , X    ,        ,             , X
      //         , lRoot, bEmpty , lScript file, eFileName, bSave
      //      X  , dDir ,   -    , xFileContent, --
      //         , +    ,        , bTest, --
      //
      //#data#
      //
      //   <lRoot> //(from /mnt/sdcard/gastona)
      //   <bEmpty> load Hello template
      //   <bSave> Save script on device
      //   <bTest> Test/execute script on device
      //
      //   <eFileName onkeyup>
      //      //AJAXgetDataForId ("checkFileName", 'fileName=' + getIdValue("eFileName"), true);
      //
      //   <eFileName>  //
      //
      //   <xFileContent>  //
      //
      //   <xFileContent onkeyup>
      //      //setToDirty (true);
      //
      //   <dDir>
      //      //<p> Loading contents ... </p>
      //
      //#listix#
      //
      //   <main>
      //      //AJAXgetDataForId ("dDir", "dir=.", true);
      //
      //   <-- ajaxResponse getDataForId>
      //      //updateFileNameCheck ();
      //      //trazos2D ().renderClassGraffiti (data);
      //
      //   <-- bSave>
      //      //AJAXPost ("savegast", getIdValue ("xFileContent"), { "ajaxREQ-gastName": getIdValue("eFileName") });
      //
      //   <-- bTest>
      //      //AJAXPost ("testgast", getIdValue ("xFileContent"));
      //
      //   <-- bEmpty>
      //      //loadAndOverwriteFileContent ('@<SPECIAL_HELLO>');

   <intro>
      //
      // programMe allows to program your android device
      // from a browser in another connected device (e.g. PC via WiFi).
      // You can load, edit, test and save scripts remotely
      //
      // 1. Open a browser in your PC with the url and port (SHOW CONNECTION URL)
      //
      // All scripts currently in your device should be visible
      //
      // 2. Press load on one script will load its source code on your browser
      //
      // You can modify it or just create a new one in the textarea
      //
      // 3. Pressing "test" will send the current script and execute it on the device
      //
      // 4. Pressing "save" will store the script on the given name (use it with care to not
      //    overwrite some script unintentionally!)
      //

   <graffiti data>
      //
      // var data = {
      //      "gFolder graffiti": [
      //           [ "defstyle", "naranja", "#ff8b1a" ],
      //           [ "z",322,399,"naranja","jauz",16,-53,27,-77,57,-15,49,5,53,7,45,12,15,22,-6,35,-16,64,-28,53,-46,4,-45,-3,-78,-15,-45,-22 ],
      //           [ "z",317,420,"naranja","jauz",-20,-63,-13,-75,56,-5,107,15,30,6,23,32,28,70,7,51,-35,10,-47,-2,-58,-12,-48,-12 ],
      //           ],
      //
      //      "gText graffiti": [
      //           [ "z",391,369,"yellow","jau",0,-95,16,-53,21,-31,20,-16,37,0,51,3,31,10,-22,42,-23,81,-3,74,6,28,-25,8,-69,-3,-25,-12,-11,-30 ],
      //           [ "z",440,209,"yellow","jau",26,-9,26,2,19,8 ],
      //           [ "z",470,217,"yellow","jau",12,0,19,8,15,-1 ],
      //           [ "z",418,240,"yellow","jau",27,-3,25,5 ],
      //           [ "z",440,269,"yellow","jau",51,3,20,-3 ],
      //         ],
      //      };
      //

   <programMe_CSS>
      //* {-webkit-box-sizing: border-box;-moz-box-sizing: border-box;box-sizing: border-box;
      //  font: 14px/bold  'Consolas', 'Monaco', 'Menlo', 'Ubuntu Mono', 'source-code-pro', monospace;
      //}
      //
      //#dirlabel { font: 18px/normal  'Tahoma', 'Verdana', 'Arial' }
      //
      //textarea {
      //   font: 11px/normal  'Consolas', 'Monaco', 'Menlo', 'Ubuntu Mono', 'source-code-pro', monospace;
      //   color: #A9B3B3;
      //   background-color: #002B36;
      //   padding: 4px;
      //}
      //
      //button {
      //   -moz-box-shadow:inset 0px 1px 0px 0px #caefab;
      //   -webkit-box-shadow:inset 0px 1px 0px 0px #caefab;
      //   box-shadow:inset 0px 1px 0px 0px #caefab;
      //   filter:progid:DXImageTransform.Microsoft.gradient(startColorstr='#77d42a', endColorstr='#5cb811',GradientType=0);
      //   background-color:#77d42a;
      //   -moz-border-radius:6px;
      //   -webkit-border-radius:6px;
      //   border-radius:6px;
      //   border:1px solid #268a16;
      //   display:inline-block;
      //   cursor:pointer;
      //   color:#306108;
      //   font-family:Arial;
      //   font-size:17px;
      //   font-weight:bold;
      //   text-decoration:none;
      //   text-shadow:0px 1px 0px #aade7c;
      //}
      //
      //button:hover {
      //   filter:progid:DXImageTransform.Microsoft.gradient(startColorstr='#5cb811', endColorstr='#77d42a',GradientType=0);
      //   background-color:#5cb811;
      //}
      //
      //button:active {
      //   position:relative;
      //   top:1px;
      //}
      //
