#gastona#

   <LOG WAKEUP>

   <fusion>
      META-GASTONA/utilApp/editor/EmergencyEditor.gasti
      META-GASTONA/utilApp/editor/javaErrorsClickAndGo.gasti

#javaj#

   <frames>
         main, Generador de gastona.jar, 700, 600

   <layout of main>
         EVA, 10 , 10, 5, 5

         ,               , X
         , lJavac path    , eJavacPath, bGenerateJar
         , lBinary archive, eBinaryArchive,  kRemove temp
      X  , o1Consola      , - , -
      X  , layJavaErrorsClickAndGo, - , -

#data#

   <gastonaVersion>  //1.2.20021725 \"fixEmpassa\"

   <kRemove temp selected> 1
   <manifestClass> gastona.gastona
   <jarName>       gastona.jar

   <manifestFile>
      //Manifest-Version: 1.0
      //Main-Class: @<manifestClass>
      //Created-By: Wakeupthecat (host @<:lsx host name> / user @<:sys user.name>)

   <traspasosTo>
      fromSubDir            , toSubDir

      META-GASTONA/img      , javaj/img
      META-GASTONA/js
      META-GASTONA/javaj
      META-GASTONA/muTools
      META-GASTONA/WelcomeGastona
      META-GASTONA/utilApp/arces
      META-GASTONA/utilApp/logAnalysis
      META-GASTONA/utilApp/gastAnalyzer
      META-GASTONA/utilApp/editor
      META-GASTONA/utilApp/std
      META-GASTONA/utilApp/xmelon

   <table task javac>

      source

      //pc/src/gastona/gastona.java
      //base/src/de/elxala/Eva/util/EvaFileToHtml.java
      //base/src/javaj/widgets/*.java


   <! ----- Version class to generate
   <gastVer>
      //package org.gastona;
      //
      /////gastona.jar built at @<:lsx date>
      //public class gastonaVersion
      //{
      //   public static String getVersion ()
      //   {
      //      return "@<gastonaVersion>";
      //   }
      //
      //   public static String getBuildDate ()
      //   {
      //      return "@<:lsx date>";
      //   }
      //
      //   public static void main (String [] par)
      //   {
      //      System.out.println (getVersion ());
      //   }
      //}
      //

   <4WINDOWS>
      VAR=, eJavacPath,     //\pavoTools\jdk1.7.0_04\bin\javac.exe
      VAR=, eBinaryArchive, //\pavoTools\gastonaBinaries
      VAR=, """", //"
      VAR=, SEP, //;

   <4LINUX>
      VAR=, eJavacPath,     ///usr/bin/javac
      VAR=, eBinaryArchive, //~/gastonaJarArchiv
      VAR=, """", //
      VAR=, SEP, //:

#listix#

   <! ------- CONSTANTS AND VARIABLES
   <! -------
   <BASE DIR GASTONA> //../../
   <PATH FOR SOURCES> //@<BASE DIR GASTONA>/pc/src@<SEP>@<BASE DIR GASTONA>/base/src
   <destDir>          //@<:sys java.io.tmpdir>jarraTMP4_@<jarName>_DIR
   <manifestSource>   //@<destDir>/META-INF/MANIFEST.MF
   <javac options>    //-nowarn

   <main0>
      LOAD, data, "@<:sys user.home>/.gastonaLocal"
      LSX, adaptOs

   <adaptOs>
      CHECK, LINUX, LSX, 4WINDOWS
      LSX, 4LINUX

   <-- bGenerateJar>
      MSG, o1Consola clear
      LSX, javaErrorsClickAndGo::clear
      LSX, generate jar file
      LSX, javaErrorsClickAndGo::parseErrors

   <generate jar file>
      'Using temporary directory [@<destDir>]
      '
      'GENERATING GASTONA VERSION [@<:lsx date>] ...
      '
      GEN, @<destDir>/org/gastona/gastonaVersion.java, gastVer
      'GENERATING jar manifest file
      '
      GEN, @<manifestSource>, manifestFile
      'MONTANDO ...
      '
      '@<copias>
      LOOP, FILES, @<BASE DIR GASTONA>, txt
          , RECURSE, 0
          ,, FILEUTIL, COPY, @<fullPath>, @<destDir>/@<fileName>

      'COMPILANDO ...
      '
      '@<task javac>
      'JARANDO ...
      '
      ZIP, ZIP, ../@<jarName>, @<destDir>

      'COPY BINARY ARCHIVE ...
      '
      FILEUTIL, COPY, ../@<jarName>, @<eBinaryArchive>/@<:lsx date yyyyMMdd>_@<jarName>
      '
      'ACABADO!
      '
      LSX, informe java no compilados
      IF, @<kRemove temp selected>
        , 1, LSX, DESTROY DESTDIR

   <DESTROY DESTDIR>
      CHECK, LINUX, CALL, //CMD /C rmdir /S/Q "@<destDir>"
      CALL, //rm -rf @<destDir>

   <informe java no compilados>
      SCAN, ADD FILES,, @<destDir>, +E, class
      SCAN, ADD FILES,, @<BASE DIR GASTONA>/base/src, +E, java
          , ROOTLABEL, base
      SCAN, ADD FILES,, @<BASE DIR GASTONA>/pc/src, +E, java
          , ROOTLABEL, src
      //
      //Lista de ficheros java no usados de base:
      //
      LOOP, SQL,, //@<java names base> EXCEPT @<class names>
          ,, //  @<name>.java
      //
      //Lista de ficheros java no usados de pc:
      //
      LOOP, SQL,, //@<java names pc> EXCEPT @<class names>
          ,, //  @<name>.java


   <class names>        //SELECT SUBSTR(subpath, 1, LENGTH(subpath)-6) AS name FROM scan_all WHERE extension = "class"
   <java names base>    //SELECT SUBSTR(subpath, 1, LENGTH(subpath)-5) AS name FROM scan_all WHERE extension = "java" AND rootLabel = "base"
   <java names pc>      //SELECT SUBSTR(subpath, 1, LENGTH(subpath)-5) AS name FROM scan_all WHERE extension = "java" AND rootLabel = "pc"

   <copias>
      LOOP, EVA, traspasosTo
         ,, IF, @<toSubDir>,
         ,,   , ""         , VAR=, copyTargetDir, @<fromSubDir>
         ,,   , ELSE       , VAR=, copyTargetDir, @<toSubDir>
         ,, LOOP, FILES, @<BASE DIR GASTONA>/@<fromSubDir>
         ,,     ,, FILEUTIL, COPY, @<fullPath>, @<destDir>/@<copyTargetDir>/@<fullSubPath>

   <compila java>
      //@<eJavacPath> -sourcepath @<">@<destDir>@<">@<SEP>@<PATH FOR SOURCES> -classpath @<">@<destDir>@<"> -d @<">@<destDir>@<"> @<javac options>

   <task javac>
      LOOP, EVA, table task javac
          ,, //*** JAVAC: @<compila java> @<BASE DIR GASTONA>/@<source>
          ,, //
          ,, CALL, //@<compila java> @<BASE DIR GASTONA>/@<source>


#**#
