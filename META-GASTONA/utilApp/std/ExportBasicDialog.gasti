#javaj#

   <layout of layExportBasic>
        EVA, 5, 5, 5, 5

           ,  X,, X
           , eExportBasicFields, rgExportBasicType, ePostfix, bExportBasic, kTemp

#data#

   <kTemp selected> 0

   <rgExportBasicType orientation> X
   <rgExportBasicType>
      label  , ext , headerDB, headerSQL, separ, preRow, postRow, head, tail

      Text+Info, txt , 1, 1, ", "
      Text     , txt , 0, 0, ", "
      Excel    , xls , 0, 0, @<TAB>
      CSV      , csv , 0, 0, """, """, """", """"
      Html     , html, 0, 0, "<td>", "<tr><td>", "</tr>", @<ExportBasic headHtml>, @<ExportBasic tailHtml>

   <TAB> @<:hex 09>

   <bExportBasic> Export

#listix#

   <-- rgExportBasicType>
      LSX, ExportBasic showFields

   <-- bExportBasic>
      LSX, ExportBasic doExport
      LAUNCH, //CMD /C "@<ExportBasic fileName>"

   <ExportBasic set (gridName dbName)>
      VAR=, ExportBasic gridName, @<p1>
      VAR=, ExportBasic gridDBName, @<p2>
      <! DUMP, data,, ExportBasic
      -->, rgExportBasicType select!, #, 0
      -->, ePostfix data!,, //reports/@<:lsx date yyyyMMdd_HHmmss>
      LSX, ExportBasic showFields

   <ExportBasic sql base>
      CHECK, VAR, @<ExportBasic gridName> sqlSelect, SELECT "" AS nocolumn
      LSX, @<ExportBasic gridName> sqlSelect

   <ExportBasic sql complete>
      //SELECT * FROM
      //       (@<ExportBasic sql base>)
      CHECK, VAR, @<ExportBasic gridName> sqlExtraFilter
      //
      "      "
      LSX, @<ExportBasic gridName> sqlExtraFilter

   <ExportBasic showFields>
      -->, eExportBasicFields data!,, @<ExportBasic showFields.gen>

   <ExportBasic showFields.gen>
      LOOP, SQL, @<ExportBasic gridDBName>, SELECT * FROM (@<ExportBasic sql base>) LIMIT 1
          ,, LOOP, COLUMNS
          ,,     , LINK, @<rgExportBasicType selected.separ>
          ,,     ,, @<@><@<columnName>>

   <ExportBasic chooseFileName>
      IN CASE, @<kTemp selected>
             ,    1, LSX, :lsx tmp @<rgExportBasicType selected.ext>
             , ELSE, //@<ExportBasic DBNAME>_@<ePostfix>.@<rgExportBasicType selected.ext>

   <ExportBasic DBNAME>
      CHECK, =, "", @<ExportBasic gridDBName>, @<ExportBasic gridDBName>
      @<:sys gastona.defaultDB>

   <ExportBasic doExport>
      <! DUMP, data,, ExportBasic
      VAR=, ExportBasic fileName, @<ExportBasic chooseFileName>
      GENERATE, @<ExportBasic fileName>, ExportBasic formatGen

   <ExportBasic formatGen>
      IN CASE, @<rgExportBasicType selected.headerDB>
             ,1 , //  DB : @<ExportBasic DBNAME>
             ,1 , //
             ,1 , //
      IN CASE, @<rgExportBasicType selected.headerSQL>
             ,1 , //  SQL: @<ExportBasic sql complete>
             ,1 , //
             ,1 , //

      @<rgExportBasicType selected.head>
      LOOP, SQL, @<ExportBasic gridDBName>, @<ExportBasic sql complete>
          , HEAD, LSX, ExportBasic columNames
          ,, @<rgExportBasicType selected.preRow>@<eExportBasicFields>@<rgExportBasicType selected.postRow>
          , TAIL, @<rgExportBasicType selected.tail>

   <ExportBasic columNames>
      LSX, rgExportBasicType selected.preRow
      LOOP, COLUMNS
          , LINK, @<rgExportBasicType selected.separ>
          ,, @<columnName>
      LSX, rgExportBasicType selected.postRow
      //
      //

   <ExportBasic headHtml>
      //<html>
      //   <style> * { font-family: Consolas } </style>
      //   <table border="1" cellspacing="0" cellpadding="6" bordercolor=gray>
      //

   <ExportBasic tailHtml>
      //
      //   </table>
      //</html>
      //

#**#
